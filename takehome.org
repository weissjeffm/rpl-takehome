# -*- mode: org; -*-
#+HTML_HEAD: <link rel="stylesheet" type="text/css" href="http://www.pirilampo.org/styles/readtheorg/css/htmlize.css"/>
#+HTML_HEAD: <link rel="stylesheet" type="text/css" href="http://www.pirilampo.org/styles/readtheorg/css/readtheorg.css"/>
#+HTML_HEAD: <style> pre.src { background: black; color: white; } #content { max-width: 1000px } </style>
#+HTML_HEAD: <script src="https://ajax.googleapis.com/ajax/libs/jquery/2.1.3/jquery.min.js"></script>
#+HTML_HEAD: <script src="https://maxcdn.bootstrapcdn.com/bootstrap/3.3.4/js/bootstrap.min.js"></script>
#+HTML_HEAD: <script type="text/javascript" src="http://www.pirilampo.org/styles/lib/js/jquery.stickytableheaders.js"></script>
#+HTML_HEAD: <script type="text/javascript" src="http://www.pirilampo.org/styles/readtheorg/js/readtheorg.js"></script>
#+HTML_HEAD: <link rel="stylesheet" type="text/css" href="styles.css"/>
#+TODO: TODO(t) INPROGRESS(i@/!) | DONE(d!) CANCELED(c@)

#+TITLE: Take Home
#+AUTHOR: Jeff Weiss
#+BABEL: :cache yes
#+OPTIONS: toc:4 h:4
#+STARTUP: showeverything

* Clojure interpreter for stack-based DSL
** Problem statement
*** Explanation of functionality
+ Variables are symbols prefixed with "!", e.g. !v
+ A stack function is defined with "defstackfn". The first argument is the input declaration
  which also provides variable names to the arguments. The stack always starts empty.
+ The implementation of a stackfn is a sequence of stack operations.
+ Using a constant as a stack operation pushes that value onto the stack
+ Using a variable as a stack operation pushes the value for that variable onto the stack.
+ A variable is assigned the top value of the stack by appending =+= to the variable name,
  e.g. =!v+=
+ A function is invoked with =invoke>=. =invoke>= takes as input the operation and the
  arity to use.
+ =<pop>= is a special operation which removes the top value of the stack.
+ =if>= tests if the top value of the stack is truthy to determine which branch to follow. The
  branches are separated with =else>=
*** Notes about your implementation
+ =defstackfn= should be a macro that produces a function invokable like any other
  function.
+ Should provide an informative error if there's an invalid stack operation or a variable is
  referenced that doesn't exist.
+ The example below contains all functionality you need to implement.
+ It should be possible to shadow vars (naming a new local the name of an existing
  variable).
*** Example invocation
#+begin_src clojure
(defstackfn f
  [!a !b !c] ; example uses input: 1 2 4. Stack starts empty.
  !a ; 1
  !b ; 1 2
  (invoke> + 2) ; 3
  !v1+ ; 3
  !c ; 3 4
  !c ; 3 4 4
  <pop> ; 3 4
  2 ; 3 4 2
  (invoke> * 2) ; 3 8
  !v2+ ; 3 8
  (invoke> = 2) ; false
  (if> ; stack empty
    !v1
    !v2
    (invoke> - 2)
    else>
    "false!!" ;; "false!!"
    (invoke> println 1) ; nil
    <pop> ; stack empty
    !v1 ; 3
    !v2 ; 3 8
    (invoke> * 2) ; 24
    )
  )
;; (f 1 2 4) prints "false!!" and returns 24
#+end_src
** Part 2 Problem statement
Modify your first project by designing and implementing:

1. Virtual method invokes, e.g. invoking "List/get" on a list and an
   arg on the stack (feel free to use reflection)
2. Anonymous stack functions (defined as part of a defstackfn) with
   lexical closures.
3. Multi-arity stack functions, including varargs
4. Loops, including “break” and “continue” operations

Feel free to change the existing API as necessary to support these
additional features.
** Design
*** Basic approach
We'll compile the various statements into forms. Each successive form
will have the state threaded through it with the =->= macro, and its
result will be a new state. We'll wrap a function definition around
that.
*** Variable shadowing
For now there is no variable scoping, and therefore setting a variable
twice will result in clobbering the previous value. Adding scoping
within an =if>= block would be pretty trivial, as the compiled form
still has access to the original variable values, and could return
those as part of the resulting state instead of whatever was created
in the =if>= block. As to which is the better design, it's hard to say
given the extreme simplicity of the language. Scoping is not strictly
needed - the stack provides a similar ability to shadow values (by
placing a new value on ToS and later popping it off).
*** Loops
It should be fairly straightforward to compile a =loop= word into a
clojure =loop=. The part that doesn't easily fit seems to be
=break=. In a clojure =loop=, there is no explicit =break=. Instead
the default is to stop whenever the body terminates without a
=recur=. I think we can simulate =break= as follows:

#+begin_src clojure
(loop [state state
       continue? true] ;; this boolean added by compiler, not exposed to stack lang
  (when (and continue?
             (-> state :stack seq) ;; should really throw stack underflow if this is false
             (-> state :stack first))
    ;; compiled loop body here
    ;; a 'break' would be expanded into
    (recur state false)
    ;; a continue would be expanded into
    (recur state true)
    ;; and the end of the loop would add to repeat the loop by default,
    ;; only exiting when the top stack item is falsey.
    (recur state true)))
#+end_src

The problem with the above is that you can't have the last recur all
the time, if there's a recur already somewhere nested in the
body. Then that inner recur is no longer in tail position and the
clojure compiler doesn't allow it.

There is an approach to deal with this problem:
**** Rewrite the body so that each branch ends with its own recur
#+begin_src
(loop> x (if> break else> 2) 3)
;; equivalent to
(loop> x (if> break else> 2 3))
;; because the break guarantees the 3 can only be reached in the false branch

rewrite an if inside a loop that contains break to move everything after the form
into the else branch. Then place a =(recur true)= at the end of the else branch to
continue.
#+end_src
#+begin_src clojure
x
(if ..
    state ;; break
    (-> state ((push 2) (push 3) (recur true))))
#+end_src

it gets more complex if there are multiple ifs:

#+begin_src
(loop x (if> 6 break else> 2) 5 (if 7 break else> 4) 11)
#+end_src

#+begin_src clojure
x 
(if ..
 (-> (push 6) (recur false))
 (-> (5 (if .. (-> 7 (recur false)) (-> 4 11 (recur true))) 
#+end_src

we could now just leave out the (recur false)s:

#+begin_src clojure
x 
(if ..
 (-> (push 6))
 (-> (5 (if .. (-> 7) (-> 4 11 (recur true)))
#+end_src

Should we rewrite the stackish and then compile?
#+begin_src 
(loop x (if> 6 break else> 2 5 (if> 7 break> else> 4 11)))
#+end_src

I think that is the way to go.
*** Anonymous stack functions
The basic idea here is to compile something like
#+begin_src 
(fn> 3 (invoke> + 2))
#+end_src

into
#+begin_src clojure
(fn [state]
  (-> state
      ;; expansion of the body
      ))
#+end_src

Then we'd have the ability to place function objects on the stack, but
still lack the ability to call them. Then we'd need a word =call>=
that invokes the function on the ToS.

#+begin_src clojure
(let [f (-> state :stack first)] ;; handle popping and stackunderflow here
    (f state))
#+end_src
*** Multi-arity stack functions
I am not sure what this means, since a "stack function" is a function
of one argument: the stack. Perhaps this means you can implement the
word =call>= to take additional arguments beyond the stack. This seems
kind of silly, since you could just push the items onto the stack and
then call the function. It would be the difference between
#+begin_src clojure
(defstackfn foo ... 1 2 call>)
#+end_src
and
#+begin_src clojure
(defstackfn foo ... (call> 1 2))
#+end_src

Maybe instead of taking arguments directly, the =call>= form will just
take an argument count, which represents how many items to take from
the stack and apply to the function. Even better would be to take the
argument count from the stack (which would restore multi-arity
functionality), but this seems at odds with the design of =invoke>=.

Having words that take varying numbers of items from the stack is
generally considered bad form in stack-based langs, but =invoke>=
already violates that so I think we can ignore this issue.

Let's go ahead and implement as-requested (with args passed in the
invocation instead of via the stack).

It would probably be the most efficient to compile directly to
clojure's multi-arity functions (which would automatically handle
calling the correct arity). The trickier part then is assigning the
variables:

#+begin_src clojure
(fn [!x !y & !z]
  ...)
#+end_src

Here, the =...= code we generate can have access to the arglist. So we
can assign variables directly and do special handling for varargs,
which just involves removing the ampersand (the rest of the values
will be constants so we can easily pass them as an evaluated list).
** Implementation
*** Project file
#+begin_src clojure :tangle project.clj
(defproject redplanetlabs "0.1.0-SNAPSHOT"
  :description "FIXME: write description"
  :url "http://example.com/FIXME"
  :license {:name "EPL-2.0 OR GPL-2.0-or-later WITH Classpath-exception-2.0"
            :url "https://www.eclipse.org/legal/epl-2.0/"}
  :dependencies [[org.clojure/clojure "1.10.0"]]
  :repl-options {:init-ns redplanetlabs.core})
#+end_src
*** Data structures
Our interpreter will need to keep track of two things:
+ the stack
+ variables

The stack is easily implemented as a clojure list, and the variables
can be a map with symbol keys. The symbols will be the names of the
variables (with decorations =!= and =+= stripped).

#+begin_src clojure :tangle src/redplanetlabs/core.clj :results none
(ns redplanetlabs.core
  (:require [clojure.pprint]
            [clojure.string :as string])
  (:refer-clojure :exclude [pop]))

(defrecord State [stack vars])

(defn new-state []
  (State. (list) {}))
#+end_src

*** Compiler functions and runtime functions
There are functions that create clojure code from =defstackfn= items
and functions that do the work at runtime. The runtime functions are
sometimes trivial but make compiled code more readable. 
#+begin_src clojure :tangle src/redplanetlabs/core.clj
;; Constants for all the language builtins
(def if-sym 'if>)
(def else-sym 'else>)
(def loop-sym 'loop>)
(def break-sym 'break>)
(def pop-sym '<pop>)
(def invoke-sym 'invoke>)
(def call-sym 'call>)
(def fn-sym 'fn>)
(def continue-sym 'continue>)

(defn debug
  "Prints out the state before returning it"
  [state]
  (doto state println))

(def ^:dynamic *debug* false)

(defmacro -debug>
  "Optionally compile in debug statements to print state during each
   step of execution. *debug* must be set to true before
   macroexpansion, so can either redef it or eg (binding [*debug*
   true] (eval '(defstackfn ...)))"
  [& body]
  (let [body (if *debug*
               (interpose `debug body)
               body)]
    `(-> ~@body)))

(defn parse-var-reference
  "Returns a pair or nil. In the pair, the first item is a symbol for
   the variable being referred to (either a get '!v' or set '!v+'
   operation). The second item is a boolean, true for a 'set'
   operation and false for a 'get' operation. Returns nil if it's not
   a proper reference.

   eg !v => [v false], !v+ => [v true], v => nil"
  [sym]
  (let [[_ var set?] (re-find #"^!([^+]+)(\+?)$" (name sym))
        set? (boolean (seq set?))]
    (and var [(symbol var) set?])))

(defn push
  "Push val onto the stack, return the state"
  [state val]
  (update state :stack conj val))

(defn compile-constant
  "Pushes the constant onto the stack"
  [constant]
  `(push ~constant))

(defn throw-error
  "Throws an exception with the format args applied to the given
   format-string as the message"
  [fmt-str & fmt-args]
  (throw (Exception. (apply format fmt-str fmt-args))))

(defn var-lookup
  "Returns the value of a variable or throws exception if it doesn't
  exist"
  [state v]
  (-> state
      :vars
      (get v)
      (or (throw-error "Variable does not exist: %s" (name v)))))

(defn set-var
  "Sets the variable var with the value currently on top of
  stack (tos). Does not change the stack."
  [{:keys [stack] :as state} var]
  (if-let [tos (first stack)]
    (update state :vars assoc var tos)
    (throw-error "Stack underflow setting variable: %s" (name var))))

(defn get-var
  "Gets the variable var and pushes it onto the stack."
  [{:keys [vars] :as state} var]
  (->> var
       (var-lookup state)
       (update state :stack conj)))

(defn pop-item
  "Returns pair of [state, ToS] where state's stack has ToS popped
   off. Throws error on empty stack."
  [{:keys [stack] :as state}]
  (if (seq stack)
    (let [tos (-> state :stack first)
          newstate (update state :stack rest)]
      [newstate tos])
    (throw-error "Stack underflow")))

(defn pop
  "Drops the top item on the stack"
  [state]
  (first (pop-item state)))

(defn compile-symbol
  "Emits code that handles symbols (variable get/set, pop)"
  [sym]
  (condp = (-> sym name symbol) ;; remove namespacing
    pop-sym `(pop)
    continue-sym '(recur) ;; normalization guarantees this will be only
    ;; in the tail position
    (if-let [var (parse-var-reference sym)]
      (let [[var set?] var]
        `(~(if set? `set-var `get-var) (quote ~var)))
      (if (= sym break-sym)
        (throw-error "break> is only allowed in conditionals inside a loop")
        (throw-error "Unknown variable or symbol: %s" (name sym))))))

(defn invoke-instance-method
  "Returns a function that invokes the given method on the first of
  its args and passes in the rest of the args to that method."
  [method-name]
  (fn [& args]
    (clojure.lang.Reflector/invokeInstanceMethod
     (first args)
     method-name
     (into-array Object (rest args)))))

(defn invoke-static-method
  "Returns a function that invokes the given static method (in the
  form 'Class/staticMethod')"
  [method-name]
  (let [[clazz method] (string/split method-name #"/")
        clazz (-> clazz symbol resolve)]
    (fn [& args]
      (clojure.lang.Reflector/invokeStaticMethod
       clazz
       method
       (into-array Object args)))))

(defn invoke-constructor
  "Returns a function that invokes the given constructor"
  [clazzname]
  (fn [& args]
    (clojure.lang.Reflector/invokeConstructor
     (class clazzname)
     (into-array Object args))))

(defn invoke
  "Calls f with args taken from the top of the stack. Arity sets the
  number of args to take from the stack. Throws an exception if there
  aren't enough args on the stack."
  [{:keys [stack] :as state} f arity]
  (let [[args stack] (split-at arity stack)]
    (when (not= (count args) arity)
      (throw-error "Stack underflow calling %s" f))
    (->> args
         reverse ;; make the ToS the last argument to the function, so
         ;; that we can write '3 2 -' and get 1 instead of -1
         (apply f)
         (conj stack)
         (assoc state :stack))))

(defn compile-java-interop
  [sym]
  (let [s (str sym)]
    (cond
      (.startsWith s ".") `(invoke-instance-method (subs ~s 1))
      (some #{\/} s) `(invoke-static-method ~s)
      (.endsWith s ".") `(invoke-constructor (->> ~s butlast (apply str)))
      :else (throw-error "Unknown symbol: %s" (str sym)))))

(defn compile-invoke
  "Emits code for invoke>"
  [[f arity]]
  (when (or (not (integer? arity)) (neg? arity))
    (throw-error "Invalid arity: %s" arity))
  (when-not (symbol? f)
    (throw-error
     "Invalid invocation, function or method must be a symbol"
     (str f)))
  `(invoke ~(if (some-> f resolve var?)
              f
              (compile-java-interop f))
           ~arity))

(defmacro threaded-if
  "Expands from an expression that fits into the threading macro ->
   (in other words, takes the state as the first arg), to a let/if
   that returns the new state"
  [state if-clause else-clause]
  `(let [[newstate# tos#] (pop-item ~state)]
     (if tos#
       (-debug> newstate# ~@if-clause)
       (-debug> newstate# ~@else-clause))))

(declare compile-item)

(defn split-if
  "Splits an if> body into a pair, [if-clause, else-clause]"
  [items]
  (let [[if-clause [_ & else-clause]] (split-with #(not= % else-sym) items)]
    [if-clause else-clause]))

(defn compile-if
  "Emits code for if> else>"
  [items]
  (let [[if-clause else-clause] (split-if items)]
    `(threaded-if ~(map compile-item if-clause)
                  ~(map compile-item else-clause))))

(defn check-break-if-clause
  "Checks if the clause has break/continue. Returns true if it is
  last, false if not present, and throws an exception if present but
  not last"
  [clause]
  (let [kws #{'break> 'continue>}]
    (if (some kws clause)
      (if (kws (last clause))
        true
        (throw-error "Break and continue must be last in clause"))
      false)))

(defn if?
  "Returns true if the stack item is an if> expression"
  [item]
  (and (seq? item)
       (-> item first (= if-sym))))

(defn add-loop-continue
  "Adds a loop continue if it's not already there"
  [body]
  (cond-> body
    (-> body last (not= 'continue>)) (-> vec (conj 'continue>) list*)))

(defn shift-loop-breaks
  "If a loop body contains an `if` that contains a 'break/continue' in
   one branch, move the rest of the body following the `if` into the
   other branch (since that code is only reachable when that branch is
   chosen), then recurse into that joined branch. Throws exception if
   break/continue isn't in tail position"
  [body]
  (let [[up-to-if [[_ & the-if] & remaining]] (split-with #(not (if? %)) body)]
    (if the-if
      (let [[if-clause else-clause :as clauses] (split-if the-if)
            [break-cont-if? break-cont-else?] (map check-break-if-clause clauses)]
        ;; if there's a break or continue in exactly one branch
        (if (not= break-cont-if? break-cont-else?) 
          ;; do the shift
          (list* (conj (vec up-to-if)
                       (remove #{break-sym}
                               (apply concat
                                      [if-sym]
                                      (if break-cont-if?
                                        [if-clause [else-sym]
                                         (shift-loop-breaks (concat else-clause remaining))]
                                        [(shift-loop-breaks (concat if-clause remaining))
                                         [else-sym] else-clause])))))
          (if break-cont-if?
            ;; break in both positions, nothing needs changing 
            body
            ;; neither has a break, default continue after
            (add-loop-continue body))))
      ;; no branch logic, continue by default
      (add-loop-continue body))))

(defmacro threaded-loop
  "Expands from an expression that fits into the threading macro ->
   (in other words, takes the state as the first arg), to a let/loop
   that returns the new state"
  [state body]
  `(loop [state# ~state]
     (let [[newstate# tos#] (pop-item state#)]
       (if tos#
         (-debug> newstate# ~@body)
         newstate#))))

(defn compile-loop
  "Compiles a loop, which checks the ToS for truthy to
  continue. Supports 'continue' and 'break'"
  [body]
  `(threaded-loop ~(map compile-item (shift-loop-breaks body))))

(defn remove-ampersand
  "Removes the & from a list of values"
  [values]
  (remove #(= % '&) values))

(defmacro threaded-fn
  "Expands from an expression that fits into the threading macro ->
   (in other words, takes the state as the first arg), to a let/fn
   that returns the new state"
  [state bodies]
  (let [orig (gensym "orig")]
    `(let [~orig ~state]
       (push ~orig
             (fn
               ~@(for [[vars body] bodies]
                   `([state# ~@(vec vars)]
                     (let [captured# (update state# :vars merge (:vars ~orig))]
                       (assoc (-debug> captured#
                                       (assign-initial-vars
                                        ;; the quoted var symbols
                                        (quote ~(remove-ampersand vars))
                                        ;; the var symbols evaluated
                                        ;; to their actual values
                                        ~(vec (remove-ampersand vars)))
                                       ~@body)
                              :vars
                              ;; restore previous scope
                              (:vars state#))))))))))

(defn compile-fn
  "Emits code for an anonymous function object"
  [bodies]
  `(threaded-fn ~(for [[vars & body] bodies]
                   [vars (map compile-item body)])))

(defn call
  "Calls anonymous function on ToS, puts return value onto
  stack. Function and its args are consumed."
  [state args]
  (let [[state tos] (pop-item state)]
    (if (fn? tos)
      (apply tos state args)
      (throw-error "Call attempted on value that isn't a function: %s", (pr-str tos)))))

(defn compile-list
  "Emits code for a list item (if or invoke)"
  [[function & args]]
  (condp = (-> function name symbol) ;; strip namespace
    if-sym (compile-if args)
    invoke-sym (compile-invoke args)
    loop-sym (compile-loop args)
    fn-sym (compile-fn args)
    call-sym `(call (quote ~args))
    (throw-error "Unknown function: %s" function)))

(defn compile-item
  "Returns a code snippet that executes the given item inside a
  defstackfn. the form is always one that fits into the -> macro. In
  other words, the first argument to whatever function is called is
  left out. It will be filled in inside the -> macro with a form that
  returns the state that this item needs."
  [item]
  (cond
    (and (sequential? item)
         (not (vector? item))) (compile-list item)
    (symbol? item) (compile-symbol item)
    true (compile-constant item)))

(defn assign-initial-vars
  "Assigns a value to the initial variables specified in the first
  argument to defstackfn. If there are more variables specified than
  arguments, throw an error. If there are more arguments than
  variables, discard the extra arguments."
  [state vars values]
  (when (> (count vars) (count values))
    (throw-error "Not enought arguments to assign all variables"))
  (update state :vars merge
          (let [;; discard the leading ! from var names
                vars (map (comp first parse-var-reference) vars)]
            ;; match up var names to args (discarding anything leftover)
            (into {} (map vector vars values)))))

(defn stackfn-body
  "Generates the body of a defn and places the compiled program in
   it. Args-sym is the symbol for the arguments to defstackfn in the
   defn's arglist."
  [args-sym initial-vars program]
  `(-debug> (new-state)
            (assign-initial-vars (quote ~initial-vars) ~args-sym)
            ~@(map compile-item program)
            :stack))

(defmacro defstackfn*
  "Compile a function with the name name-sym, and a list of variable
  names to assign at runtime to the arguments passed to the
  function. The function accepts a variable number of args and returns
  the entire stack (helpful for debugging)."
  [name-sym initial-vars & program]
  (let [args-sym (gensym "args")]
    `(defn ~(-> name-sym name symbol) [& ~args-sym]
       ~(stackfn-body args-sym initial-vars program))))

(defmacro defstackfn
  "Compile a function with the name name-sym, and a list of variable
  names to assign at runtime to the arguments passed to the
  function. The function accepts a variable number of args and returns
  the top stack item (or nil if empty)."
  [name-sym initial-vars & program]
  (let [args-sym (gensym "args")]
    `(defn ~(-> name-sym name symbol) [& ~args-sym]
       (first ~(stackfn-body args-sym initial-vars program)))))
#+end_src

#+RESULTS:
| #'redplanetlabs.core/if-sym                 |
| #'redplanetlabs.core/else-sym               |
| #'redplanetlabs.core/loop-sym               |
| #'redplanetlabs.core/break-sym              |
| #'redplanetlabs.core/pop-sym                |
| #'redplanetlabs.core/invoke-sym             |
| #'redplanetlabs.core/call-sym               |
| #'redplanetlabs.core/fn-sym                 |
| #'redplanetlabs.core/continue-sym           |
| #'redplanetlabs.core/debug                  |
| #'redplanetlabs.core/*debug*                |
| #'redplanetlabs.core/-debug>                |
| #'redplanetlabs.core/parse-var-reference    |
| #'redplanetlabs.core/push                   |
| #'redplanetlabs.core/compile-constant       |
| #'redplanetlabs.core/throw-error            |
| #'redplanetlabs.core/var-lookup             |
| #'redplanetlabs.core/set-var                |
| #'redplanetlabs.core/get-var                |
| #'redplanetlabs.core/pop-item               |
| #'redplanetlabs.core/pop                    |
| #'redplanetlabs.core/compile-symbol         |
| #'redplanetlabs.core/invoke-instance-method |
| #'redplanetlabs.core/invoke-static-method   |
| #'redplanetlabs.core/invoke                 |
| #'redplanetlabs.core/compile-java-interop   |
| #'redplanetlabs.core/compile-invoke         |
| #'redplanetlabs.core/threaded-if            |
| #'redplanetlabs.core/compile-item           |
| #'redplanetlabs.core/split-if               |
| #'redplanetlabs.core/compile-if             |
| #'redplanetlabs.core/check-break-if-clause  |
| #'redplanetlabs.core/if?                    |
| #'redplanetlabs.core/add-loop-continue      |
| #'redplanetlabs.core/shift-loop-breaks      |
| #'redplanetlabs.core/threaded-loop          |
| #'redplanetlabs.core/compile-loop           |
| #'redplanetlabs.core/remove-ampersand       |
| #'redplanetlabs.core/threaded-fn            |
| #'redplanetlabs.core/compile-fn             |
| #'redplanetlabs.core/call                   |
| #'redplanetlabs.core/compile-list           |
| #'redplanetlabs.core/compile-item           |
| #'redplanetlabs.core/assign-initial-vars    |
| #'redplanetlabs.core/stackfn-body           |
| #'redplanetlabs.core/defstackfn*            |
| #'redplanetlabs.core/defstackfn             |

*** Tests
#+begin_src clojure :tangle test/redplanetlabs/core_test.clj :results none
(ns redplanetlabs.core-test
  (:require [clojure.test :refer :all]
            [redplanetlabs.core :as sut]))

(sut/defstackfn* example [!a !b !c] ; example uses input: 1 2 4. Stack starts empty.
  !a ; 1
  !b ; 1 2
  (invoke> + 2) ; 3
  !v1+ ; 3
  !c ; 3 4
  !c ; 3 4 4
  <pop> ; 3 4
  2 ; 3 4 2
  (invoke> * 2) ; 3 8
  !v2+ ; 3 8
  (invoke> = 2) ; false
  (if> ; stack empty
      !v1
    !v2
    (invoke> - 2)
    else>
    "false!!" ;; "false!!"
    (invoke> println 1) ; nil
    <pop> ; stack empty
    !v1 ; 3
    !v2 ; 3 8
    (invoke> * 2) ; 24
    )
  )

(sut/defstackfn* nested-if [!y]
  !y !y
  (invoke> pos? 1)
  (if> !y
    (invoke> even? 1)
    (if> 3
      (invoke> + 2)
      else>
      11
      (invoke> * 2))
    else> -99))

(sut/defstackfn* shadow [!x !y] !x !y !x+ !x)

(sut/defstackfn* looping []
  1 true
  (loop> !x+ !x ;; hacky dup
         (invoke> + 2)
         !x+ !x 
         10000 (invoke> < 2)))

(sut/defstackfn* fib [!start !max]
  !start !start true
  (loop> !a+ <pop> !b+ <pop> !a !a !b
         (invoke> + 2) ;; 1 2
         !b !b 
         !max (invoke> < 2)))

(sut/defstackfn* loop-break1 [!start]
  !start true
  (loop> !x+ !x (invoke> dec 1)
         !x+ !x 5 (invoke> < 2)
         (if> break>
           else>
           !x+ !x)
         (invoke> pos? 1)))

(sut/defstackfn* make-square [!len]
  1 !rows+ <pop> [] true
  (loop>
   [] 1 !x+ <pop> true ;; [] true
   (loop> !x ;; [] 1
          (invoke> conj 2) ;; [1]
          !x ;; [1] 1
          (invoke> inc 1) ;; [1] 2
          !x+
          !len ;; [1] 2 5
          (invoke> <= 2))
   (invoke> conj 2)
   !rows
   (invoke> inc 1)
   !rows+
   !len
   (invoke> <= 2)))

(sut/defstackfn* loop-continue [!x]
  !x
  2 (invoke> > 2) ;; if > 2 loop
  (loop>
   !x 3 (invoke> mod 2) 0 (invoke> = 2) ;; divisible by 3
   (if> !x 2 (invoke> - 2) !x+ true continue> ;; subtract 2 from x, leave item on stack
        else>
        !x 10 (invoke> - 2)) ;; subtract 10
   (invoke> dec 1) !x+ ;; dec and set x
   2 (invoke> > 2)))

(sut/defstackfn* closure []
  4 !x+
  ;; capture x as 4
  (fn> ([!z] !x 4 (invoke> = 2)
        (if> !z :yes
             27 !x+ <pop> ;; change x to 27 in inner scope
             else> :no))) 
  5 !x+ <pop> ;; change x in outer scope
  (call> :fnarg)
  ;; check that outer x is back after inner scope exits
  !x)

(sut/defstackfn* varargs [!x !a]
  (fn> ([!x] !x) ([!x & !y] !x !y))
  ;; now overwrite x
  !a
  !x+
  <pop>
  (call> 1 2 3 4))

(sut/defstackfn* multiple-call [!x]
  (fn> ([!y] !x !y (invoke> + 2)))
  !f+
  (call> 4)
  !f
  (call> 5))

(sut/defstackfn* java [!x !y]
  !x (invoke> Math/abs 1) (invoke> .toString 1) !y (invoke> .contains 2)
  (invoke> String. 0) (invoke> .length 1) 0 (invoke> = 2))

(deftest example-test
  (is (= (example 1 2 4) '(24))))

(deftest nested-if-test
  (are [y exp] (= (nested-if y) exp)
    4 '(7)
    5 '(55)
    -4  '(-99 -4)))

(deftest shadow-test
  (is (= (shadow 1 2) '(2 2 1))))

(deftest loop-test
  (is (= (fib 1 10) '(13 21 8 5 3 2 1 1)))
  (is (= (loop-break1 7 '(7 6 5 4)))))

(deftest nested-loop-test
  (is (= (make-square 3) '([[1 2 3] [1 2 3] [1 2 3]]))))

(deftest lexical-closure-test
  (is (= (closure) '(5 :yes :fnarg 4))))

(deftest multiple-call-test
  (is (= (multiple-call 11) '(16 15))))

(deftest varargs-test
  (is (= (varargs 12 19) '((2 3 4) 1))))

(deftest java-test
  (is (= (java -140 "4") '(true true)))
  (is (= (java -140 "5") '(true false))))

(deftest continue-test
  (is (= (loop-continue 30) '(4 28))))

(deftest compiler-error-tests
  (are [stackfn] (thrown-with-msg? clojure.lang.Compiler$CompilerException
                                   #"Syntax error"
                                   (eval stackfn))
    `(sut/defstackfn foo [] (unknownFn 1))
    `(sut/defstackfn foo [] unknownFn)
    `(sut/defstackfn foo [] (invoke> +)) ;; no arity
    `(sut/defstackfn foo [] (invoke> + :badarity)) 
    `(sut/defstackfn foo [] (invoke> :unknownFn 2)) ;; unresolvable 
    `(sut/defstackfn foo [] (fn> ([]) ([]))) ;; two bodies with same arity
    `(sut/defstackfn foo [] (loop> (if> 1 break> 2 else> 3)))
    `(sut/defstackfn foo [] break>) ;; break> not allowed outside loop
    `(sut/defstackfn foo [] (loop> break>)) ;; unconditional break> not allowed
    `(sut/defstackfn foo [] continue>) ;; continue not allowed outside loop
    `(sut/defstackfn foo [] ())))
    
#+end_src

*** Issues
**** DONE Have defstackfn return ToS only
It's way easier to debug and run tests the way it is now, so to fix
we'll just make defstackfn a thin wrapper around another macro and run
all the tests with the inner macro.
***** DONE Fix defstackfn macro
- State "DONE"       from "TODO"       [2022-08-21 Sun 21:41]
**** INPROGRESS Implement virtual method invokes
- State "INPROGRESS" from "TODO"       [2022-08-21 Sun 21:08]
***** TODO Implement constructor calls
**** DONE Define constants for all the languages' keywords
- State "DONE"       from "TODO"       [2022-08-17 Wed 18:35]
**** DONE Make 'break' compile out to nothing instead of no-op code
- State "DONE"       from "TODO"       [2022-08-17 Wed 18:35]
**** CANCELED Allow bare fn arity when there's only one defined
- State "CANCELED"   from "TODO"       [2022-08-22 Mon 15:50] \\
  It's too hard to tell the visual difference between a function body
  and an invocation of invoke> or if> or call>. Just keep the
  requirement that even a single function body has to be wrapped.
Like clojure's fn, if there's only one arity, don't make us wrap it in
a list.
**** DONE Varargs anonymous fns
- State "DONE"       from "INPROGRESS" [2022-08-22 Mon 15:52]
***** TODO More tests
**** INPROGRESS Write more tests
**** DONE Fix debug macro
- State "DONE"       from "TODO"       [2022-08-21 Sun 21:41]
